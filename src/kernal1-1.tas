
;
; GEOS Kernal 128 v2.0
; reassembled by Maciej 'YTM/Elysium' Witkowiak
; 31.7, 1-4,9-10.8 2000
;

;--
; zpage	; different kernal.inc
IconDescVec	= $40
CallRLo		= $42
CallRHi		= $43
DBoxDesc	= $44
DBoxDescH	= $45
Z46		= $46

;--
; equ.inc
KEY_TAB		= 9		; -> const.inc
KEY_LF		= 10		; -> const.inc
backPattern1	= $55
backPattern2	= $aa
menuVSeparator	= $aa
menuHSeparator  = $55
REUOsVarBackup	= $7900
REUDskDrvSPC	= $8300
iniMaxMouseSpeed= $7f
iniMinMouseSpeed= $1e
iniMouseAccel	= $7f
SelectFlashDelay= 10
SerialNumberValue = $1cd5
;--
; kernal.inc
PALNTSCFLAG	= $0a03
BASICspace	= $1c00
sysFlgSave	= $fff5
KbdDMltTab	= $87f6
PrvCharWidth	= $880d
clkBoxTemp	= $8819
E881A		= $881a
clkBoxTemp2	= $881b
alarmWarnFlag	= $881c
defIconTab	= $881f
DeskAccPC	= $8863	; was: 8850
DeskAccSP	= $8865	; was: 8852
dlgBoxCallerPC	= $8866
dlgBoxCallerSP	= $8868
DBGFilesFound	= $8869
DBGFOffsLeft	= $886a
DBGFOffsTop	= $886b
DBGFNameTable	= $886c
DBGFTableIndex	= $886e
DBGFileSelected	= $886f
E8870		= $8870	; was: 885D
E8871		= $8871
E8872		= $8872	; was: 885E
E8873		= $8873	; was: 885E/5F
RecordDirTS	= $8875
RecordDirOffs	= $8877
RecordTableTS	= $8879
verifyFlag	= $887b
TempCurDrive	= $887c
E887D		= $887d
E887E		= $887e
E888F		= $888f
E8890		= $8890
krnlSaveMMU	= $8894
krnlSaveConfig	= $8895
krnlSaveA	= $8896
bank0SaveMMU	= $8897
bank0SaveA	= $8898
bank0SavePS	= $8899

;--
fileHeadMemBuff	= $d8c0
prntDrvMemBuff	= $d9c0		;$0640 len -> $e000
;--
EFF3D=$ff3d			; this is in ROM
CBM_RestoreVectors=$ff8a	; this is through ExecCBMKernal
;--

		;*=$c000

		JMP BootKernal
		JMP InitKernal			;->5000

bootName	!text "GEOS BOOT"		;c006
version		!byte $20			;c00f
nationality	!byte $00, $00
sysFlgCopy	!byte $00			;c012
c128Flag	!byte $80			;c013
		!byte $04, $00, $00, $00
dateCopy	!byte $58, $08, $16		;c018

BootKernal	SEI				;c01b
		CLD
		LDX #$FF
		TXS
		+LoadB config, CIOIN
		+LoadB mmu+6, $40
		+bbsf 5, sysFlgCopy, BootREU
		STA sysFlgSave
		LDX #8
BootKernal_1	LDA EC052-1,X			;c036
		STA r0L-1,X
		DEX
		BNE BootKernal_1
		JMP r0
BootREU 	LDY #8				;c041
BootREU1	LDA BootREUTab,Y		;c043
		STA EXP_BASE+1,Y
		DEY
		BPL BootREU1
BootREU2	DEY				;c04c
		BNE BootREU2
_RunREU 	JMP RunREU			;c04f

EC052		+LoadB config, 0		;c052
		JMP EFF3D

BootREUTab					;c05a
		!word $0091
		!word $0060
		!word $007e
		!word $0500
		!word $0000

_ToBASIC					;c064
		JSR EE063
		LDA r5H
		BEQ TB3
		INY
		TYA
TB1		STA BASICspace,Y		;c06d
		INY
		BNE TB1
		+SubVW $0002,r7
		LDA (r7),Y
		PHA
		INY
		LDA (r7),Y
		PHA
		+PushW r7
		LDA (r5),Y
		STA r1L
		INY
		LDA (r5),Y
		STA r1H
		LDA #$FF
TB2		STA r2L				;c098
		STA r2H
		JSR _ReadFile
		+PopW r0
		LDY #1
		PLA
		STA (r0),Y
		DEY
		PLA
		STA (r0),Y
		+LoadB r5L, NULL
TB3		JSR NewDisk			;c0b2
		TXA
		PHA
		JSR PurgeTurbo
		+MoveB sysRAMFlg, sysFlgCopy
		AND #%00100000
		BEQ TB5
 		LDY #6
TB4		LDA ToBASICTab,Y		;c0c6
		STA r0,Y
		DEY
		BPL TB4
		JSR _StashRAM
TB5		PLA				;c0d2
		JMP EE066
		
ToBASICTab      				;c0d6
		!word dirEntryBuf
		!word REUOsVarBackup
		!word OS_VARS_LGH
		!byte $00

_MainLoop					;c0dd
		JSR _DoCheckButtons
		JSR _ExecuteProcesses
		JSR _DoCheckDelays
		JSR _DoUpdateTime
		LDA appMain+0
		LDX appMain+1
_MNLP		JSR _CallRoutine		;c0ef
		LDA grcntrl1
		AND #%01111111
		STA grcntrl1
		CLI
		JMP _MainLoop

;jump table					;c100
