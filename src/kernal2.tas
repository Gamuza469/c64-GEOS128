
;		*=$d000

_IsMseInRegion_	JMP __IsMseInRegion__
_MoveData_	JMP __MoveData__
_CRC_		JMP __CRC__
_GetRandom_	JMP __GetRandom__
_BBMult_	JMP __BBMult__
_BMult_		JMP __BMult__
_DMult_		JMP __DMult__
_Ddiv_		JMP __Ddiv__
_DSdiv_		JMP __DSdiv__
_ProcessDelays_	JMP __ProcessDelays__
_GetInlineDrwParms_	JMP __GetInlineDrwParms__
_GrStSetCoords_	JMP __GrStSetCoords__
_ProcessTimers_	JMP __ProcessTimers__

__IsMseInRegion__				;d027
		TXA
		PHA
		LDX #r3
		JSR _NormalizeX
		LDX #r4
		JSR _NormalizeX
		PLA
		TAX
		LDA mouseYPos
		CMP r2L
		BCC IMIRl4
		CMP r2H
		BEQ IMIRl0
		BCS IMIRl4
IMIRl0		LDA mouseXPos+1 		;d041
		CMP r3H
		BNE IMIRl1
		LDA mouseXPos
		CMP r3L
IMIRl1		BCC IMIRl4			;d04b
		LDA mouseXPos+1
		CMP r4H
		BNE IMIRl2
		LDA mouseXPos
		CMP r4L
IMIRl2		BEQ IMIRl3			;d057
		BCS IMIRl4
IMIRl3		LDA #true			;d05b
		RTS
IMIRl4		LDA #false			;d05e
		RTS

__MoveData__					;d061
		+CmpW r0, r1
		BCS MData2			;d06b
		BCC MData7
MData2		LDY #0				;d06f
		LDA r2H
		BEQ MData4
MData3		LDA (r0),Y			;d075
		STA (r1),Y
		INY
		BNE MData3
		INC r0H
		INC r1H
		DEC r2H
		BNE MData3
MData4		CPY r2L 			;d084
		BEQ MData5
		LDA (r0),Y
		STA (r1),Y
		INY
		+bra MData4
MData5		RTS				;d090
MData7		+AddB r2H, r0H			;d091
		+AddB r2H, r1H
		LDY r2L
		BEQ MData9
MData8		DEY				;d0a3
		LDA (r0),Y
		STA (r1),Y
		TYA
		BNE MData8
MData9		DEC r0H 			;d0ab
		DEC r1H
		LDA r2H
		BEQ MData5
MData10 	DEY				;d0b3
		LDA (r0),Y
		STA (r1),Y
		TYA
		BNE MData10
		DEC r2H
		+bra MData9

__CRC__						;d0c0
		LDY #$ff
		STY r2L
		STY r2H
		INY
CRC1		LDA #$80			;d0c7
		STA r3L
CRC2		ASL r2L 			;d0cb
		ROL r2H
		LDA (r0),y
		AND r3L
		BCC CRC3
		EOR r3L
CRC3		BEQ CRC4			;d0d7
		LDA r2L
		EOR #$21
		STA r2L
		LDA r2H
		EOR #$10
		STA r2H
CRC4		LSR r3L 			;d0e5
		BCC CRC2
		INY
		BNE CRC5
		INC r0H
CRC5		LDX #r1 			;d0ee
		JSR _Ddec
		LDA r1L
		ORA r1H
		BNE CRC1
		RTS

__GetRandom__					;d0fa
 		INC random
		BNE GRandl0
		INC random+1
GRandl0 	ASL random			;d102
		ROL random+1
		BCC GRandl2
		LDA #$0F
		+add random
		STA random
		BCC GRandl1
		INC random+1
GRandl1 	RTS				;d118
GRandl2 	+CmpBI random+1, $ff		;d119
		BCC GRandl3
		LDA random
		+subv $f1
		BCC GRandl3
		STA random
		LDA #0
		STA random+1
GRandl3 	RTS				;d130

__BBMult__					;d131
		LDA zpage,Y
		STA r8H
		STY r8L
		LDY #8
		LDA #0
BBMul0		LSR r8H 			;d13c
		BCC BBMul1
		CLC
		ADC zpage,X
BBMul1		ROR				;d143
		ROR r7L
		DEY
		BNE BBMul0
		STA zpage+1,X
		LDA r7L
		STA zpage,X
		LDY r8L
		RTS

__BMult__					;d152
		LDA #0
		STA zpage+1,Y
__DMult__	+LoadB r8L, 16			;d157
		+LoadW r7, 0
BMult0		LSR zpage+1,X			;d161
		ROR zpage,X
		BCC BMult1
		LDA r7L
		CLC
		ADC zpage,Y
		STA r7L
		LDA r7H
		ADC zpage+1,Y
BMult1		LSR				;d174
		STA r7H
		ROR r7L
		ROR r6H
		ROR r6L
		DEC r8L
		BNE BMult0
		LDA r6L
		STA zpage,X
		LDA r6H
		STA zpage+1,X
		RTS

__Ddiv__					;d18a
 		+LoadW r8, 0
		+LoadB r9L, 16
Ddivl0		ASL zpage,X			;d194
		ROL zpage+1,X
		ROL r8L
		ROL r8H
		LDA r8L
		SEC
		SBC zpage,Y
		STA r9H
		LDA r8H
		SBC zpage+1,Y
		BCC Ddivl1
		INC zpage,X
		STA r8H
		LDA r9H
		STA r8L
Ddivl1		DEC r9L 			;d1b3
		BNE Ddivl0
Ddivl2		RTS

__DSdiv__					;d1b8
 		LDA zpage+1,X
		EOR zpage+1,Y
		PHP
		JSR _Dabs
		STX r8L
		TYA
		TAX
		JSR _Dabs
		LDX r8L
		JSR __Ddiv__
		PLP
		BPL Ddivl2
		jmp _Dnegate

__ProcessDelays__				;d1d4
		LDX DelaySP
		BEQ ProcDel3
		DEX
ProcDel0	LDA DelayValL,X 		;d1da
		BNE ProcDel1
		ORA DelayValH,X
		BEQ ProcDel2
		DEC DelayValH,X
ProcDel1	DEC DelayValL,X 		;d1e7
ProcDel2	DEX				;d1ea
		BPL ProcDel0
ProcDel3	RTS				;d1ed

__GetInlineDrwParms__				;d1ee
		+PopW r6
		+PopW r5
		+PopW returnAddress
		LDY #1
		LDA (returnAddress),Y
		STA r2L
		INY
		LDA (returnAddress),Y
		STA r2H
		INY
		LDA (returnAddress),Y
		STA r3L
		INY
		LDA (returnAddress),Y
		STA r3H
		INY
		LDA (returnAddress),Y
		STA r4L
		INY
		LDA (returnAddress),Y
		STA r4H
		+PushW r5
		+PushW r6			;!!!hint??? (maybe it's to restore r6?)
		RTS

__GrStSetCoords__				;d22c
		JSR GetCoords
		CMP GraphPenY
		BCS GSSC0
		STA r2L
		PHA
		LDA GraphPenY
		STA r2H
		+bra GSSC1
GSSC0		STA r2H 			;d23f
		PHA
		LDA GraphPenY
		STA r2L
GSSC1		+PopB GraphPenY 		;d247
		CPY GraphPenXH
		BEQ GSSC2
		BCS GSSC4
GSSC2		BCC GSSC3			;d252
		CPX GraphPenXL
		BCS GSSC4
GSSC3		STX r3L 			;d259
		STY r3H
		+MoveW GraphPenX, r4
		+bra GSSC5
GSSC4		STX r4L 			;d26a
		STY r4H
		+MoveW GraphPenX, r3
GSSC5		STX GraphPenXL			;d278
		STY GraphPenXH
		RTS

__ProcessTimers__				;d27f
		LDA #0
		TAY
		TAX
		CMP NumTimers
		BEQ PTime3
PTime0		LDA TimersCMDs,X		;d288
		AND #SET_FROZEN | SET_NOTIMER
		BNE PTime2
		LDA TimersTab,Y
		BNE PTime1
		PHA
		LDA TimersTab+1,Y
		+subv 1
		STA TimersTab+1,Y
		PLA
PTime1		+subv 1 			;d29f
		STA TimersTab,Y
		ORA TimersTab+1,Y
		BNE PTime2
		JSR RProc0
		LDA TimersCMDs,X
		ORA #SET_RUNABLE
		STA TimersCMDs,X
PTime2		INY				;d2b5
		INY
		INX
		CPX NumTimers
		BNE PTime0
PTime3		RTS				;d2bd

;----------------------------------------------------------------
; DialogBox Icons
DBIcPicNO					;d2be
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+12
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %.......#, %##..##.., %.#####.., %........, %......##
	!byte %#......., %.......#, %##..##.., %##...##., %........, %......##, $db+8, 2, $80+6
	!byte %#......., %.......#, %###.##.., %##...##., %........, %......##, $db+8, 2, $80+6
	     ;%#......., %.......#, %###.##.., %##...##., %........, %......##
	!byte %#......., %.......#, %#.####.., %##...##., %........, %......##, $db+8, 2, $80+6
	     ;%#......., %.......#, %#.####.., %##...##., %........, %......##
	!byte %#......., %.......#, %#..###.., %##...##., %........, %......##, $80+6
	     ;%#......., %.......#, %#..###.., %##...##., %........, %......##
	!byte %#......., %.......#, %#...##.., %.#####.., %........, %......##, $db+8, 2, $80+6
	!byte %#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######, 5, %########

DBIcPicYES					;d309
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+(5*6)
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %....##.., %##..####, %##...###, %##......, %......## ;1
	!byte %#......., %....##.., %##..##.., %....##.., %.##....., %......## ;2
	!byte %#......., %....##.., %##..##.., %....##.., %........, %......## ;3
	!byte %#......., %.....###, %#...##.., %....##.., %........, %......## ;4
	!byte %#......., %.....###, %#...####, %#....###, %##......, %......##, $db+8, 2, $80+6
	!byte %#......., %......##, %....##.., %........, %.##....., %......##, $80+12
	     ;%#......., %......##, %....##.., %........, %.##....., %......##
	!byte %#......., %......##, %....##.., %....##.., %.##....., %......##
	!byte %#......., %......##, %....####, %##...###, %##......, %......##, $db+8, 2, $80+6
	!byte %#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######, 5, %########

DBIcPicOPEN					;d35a
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+(9*6)
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %..#####., %........, %........, %........, %......## ;1
	!byte %#......., %.##...##, %........, %........, %........, %......## ;2
	!byte %#......., %.##...##, %.#####.., %.####..#, %####...., %......## ;3
	!byte %#......., %.##...##, %.##..##., %##..##.#, %##.##..., %......## ;4
	!byte %#......., %.##...##, %.##..##., %##..##.#, %#..##..., %......## ;5
	!byte %#......., %.##...##, %.##..##., %######.#, %#..##..., %......## ;6
	!byte %#......., %.##...##, %.##..##., %##.....#, %#..##..., %......## ;7
	!byte %#......., %.##...##, %.##..##., %##..##.#, %#..##..., %......## ;8
	!byte %#......., %..#####., %.#####.., %.####..#, %#..##..., %......##, $db+8, 2, $80+6
	!byte %#......., %........, %.##....., %........, %........, %......##
	     ;%#......., %........, %.##....., %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######, 5, %########

DBIcPicDISK					;d3ad
	!byte 5, %########, $80+1, %#######., $db+8, 2, $80+6
	     ;%########, %########, %########, %########, %########, %#######.
	!byte %#......., %........, %........, %........, %........, %......##, $80+(9*6)
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %...#####, %....##.., %......##, %........, %......## ;1
	!byte %#......., %...##..#, %#......., %......##, %........, %......## ;2
	!byte %#......., %...##..., %##.###.., %####..##, %..##...., %......## ;3
	!byte %#......., %...##..., %##..##.#, %#..##.##, %.##....., %......## ;4
	!byte %#......., %...##..., %##..##.#, %#.....##, %##......, %......## ;5
	!byte %#......., %...##..., %##..##.., %####..##, %#......., %......## ;6
	!byte %#......., %...##..., %##..##.., %...##.##, %##......, %......## ;7
	!byte %#......., %...##..#, %#...##.#, %#..##.##, %.##....., %......## ;8
	!byte %#......., %...#####, %....##.., %####..##, %..##...., %......##, $db+8, 2, $80+6
	!byte %#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte 6, %########, $80+1, %.#######


DBIcPicCANCEL					;d3fe
	!byte 5, %########, $80+2, %#######.
	!byte %#......., 4, %........, $80+2, %......##
	!byte %#......., 4, %........, $80+(9*6)+2, %......##
	     ;%########, %########, %########, %########, %########, %#######.
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#....###, %##......, %........, %........, %........, %###...## ;1
	!byte %#...##.., %.##....., %........, %........, %........, %.##...## ;2
	!byte %#...##.., %.....###, %#..#####, %...####., %..####.., %.##...## ;3
	!byte %#...##.., %....##.., %##.###.#, %#.##..##, %.##..##., %.##...## ;4
	!byte %#...##.., %.....###, %##.##..#, %#.##...., %.##..##., %.##...## ;5
	!byte %#...##.., %....##.., %##.##..#, %#.##...., %.######., %.##...## ;6
	!byte %#...##.., %....##.., %##.##..#, %#.##...., %.##....., %.##...## ;7
	!byte %#...##.., %.##.##.., %##.##..#, %#.##..##, %.##..##., %.##...## ;8
	!byte %#....###, %##...###, %##.##..#, %#..####., %..####.., %.##...## ;9
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte %#......., 4, %........, $80+2, %......##
	!byte %#......., 4, %........, $80+1, %......##
	!byte 6, %########, $80+1, %.#######

DBIcPicOK					;d450
	!byte 5, %########, $80+2, %#######.
	!byte %#......., 4, %........, $80+2, %......##
	!byte %#......., 4, %........, $80+(9*6)+2, %......##
	     ;%########, %########, %########, %########, %########, %#######.
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	!byte %#......., %........, %#####..., %##...##., %........, %......## ;1
	!byte %#......., %.......#, %#...##.., %##..##.., %........, %......## ;2
	!byte %#......., %.......#, %#...##.., %##.##..., %........, %......## ;3
	!byte %#......., %.......#, %#...##.., %####...., %........, %......## ;4
	!byte %#......., %.......#, %#...##.., %###....., %........, %......## ;5
	!byte %#......., %.......#, %#...##.., %####...., %........, %......## ;6
	!byte %#......., %.......#, %#...##.., %##.##..., %........, %......## ;7
	!byte %#......., %.......#, %#...##.., %##..##.., %........, %......## ;8
	!byte %#......., %........, %#####..., %##...##., %........, %......## ;9
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%#......., %........, %........, %........, %........, %......##
	     ;%########, %########, %########, %########, %########, %########
	     ;%.#######, %########, %########, %########, %########, %########
	!byte %#......., 4, %........, $80+2, %......##
	!byte %#......., 4, %........, $80+1, %......##
	!byte 6, %########, $80+1, %.#######, 5, %########

DBGFArrowPic					;d4a4
	!byte 3, %########, $80+(10*3)
	     ;%########, %########, %########
	!byte %#......., %........, %.......# ;1
	!byte %#......., %........, %.......# ;2
	!byte %#.....#., %........, %###....# ;3
	!byte %#....###, %.....###, %######.# ;4
	!byte %#...####, %#.....##, %#####..# ;5
	!byte %#..#####, %##.....#, %####...# ;6
	!byte %#.######, %###....., %###....# ;7
	!byte %#....###, %........, %.#.....# ;8
	!byte %#......., %........, %.......# ;9
	!byte %#......., %........, %.......# ;10
	     ;%########, %########, %########
	!byte 3, %########

; d4c7

;printer driver and fileHeader buffers follow starting @ $d8c0
